// Forge/Neoforge build.gradle
plugins {
    id 'com.gradleup.shadow'
}
println "actual_version property: " + rootProject.findProperty('actual_version')


architectury {
    platformSetupLoomIde()
    neoForge{
        platformPackage = "forge"
    }
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    neoForge "net.neoforged:neoforge:${neo_version}"
    //forge "net.minecraftforge:forge:$rootProject.forge_version"

    // Architectury API. This is optional, and you can comment it out if you don't need it.
    modImplementation "dev.architectury:architectury-neoforge:$rootProject.architectury_api_version"
    modImplementation "net.puffish:skillsmod:${skill_api}:neoforge"


    // Add implementation for the libraries
    implementation "org.yaml:snakeyaml:${snake_version}"
    implementation "com.github.spacemex:SimpleConfigApi:${simple_config_version}"

    // Add them for shading and relocation
    shadow "org.yaml:snakeyaml:${snake_version}"
    shadow "com.github.spacemex:SimpleConfigApi:${simple_config_version}"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionNeoForge')
}

processResources {
    def actualVer = rootProject.findProperty('actual_version') ?: 'undefined'

    inputs.property 'version', project.version
    inputs.property 'actual_version', actualVer

    filesMatching('META-INF/neoforge.mods.toml') {
        expand(
                version: project.version,
                actual_version: actualVer
        )
    }
}


shadowJar {
    configurations = [project.configurations.shadowBundle, project.configurations.shadow]

    // Relocate packages to avoid conflicts
    relocate 'org.yaml.snakeyaml', 'com.github.spacemex.skillexpnotifier.libs.snakeyaml'
    relocate 'com.github.spacemex.SimpleConfigApi', 'com.github.spacemex.skillexpnotifier.libs.simpleconfigapi'

    archiveClassifier = 'dev-shadow'
}

remapJar {
    inputFile.set shadowJar.archiveFile
}